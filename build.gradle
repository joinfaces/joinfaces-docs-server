plugins {
    id "java"
    id "org.springframework.boot" version "3.5.3"
    id "io.freefair.git-version"
    id "io.freefair.lombok"
    id "io.freefair.javadoc-links"
    id "io.freefair.okhttp"
}

apply plugin: 'io.spring.dependency-management'

group = 'org.joinfaces.docs'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

bootJar {
    launchScript()
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    implementation 'org.apache.tika:tika-core:3.2.1'

    implementation 'org.webjars:webjars-locator-core'
    implementation 'org.webjars:bootstrap:5.3.5'
    implementation 'org.webjars:font-awesome:6.7.2'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

task uploadDocumentation(type: io.freefair.gradle.plugins.okhttp.tasks.UploadFile) {
    dependsOn "javadocJar"
    username = "user"
    password = findProperty('joinfacesDocsPass')
    file = javadocJar.archiveFile
    url = "https://docs.joinfaces.org/api/$project.version?path=joinfaces-docs-server"
}
